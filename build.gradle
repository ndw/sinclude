plugins {
  id "java"
  id 'com.github.gmazzo.buildconfig' version "2.0.2"
}

sourceSets {
  main {
    resources {
      exclude '**'
    }
  }	
}

repositories {
  mavenLocal()
  mavenCentral()
}

configurations.all {
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01'
  }
}

configurations {
}

dependencies {
  implementation (
    [group: 'net.sf.saxon', name: 'Saxon-HE', version: saxonVersion],
    [group: 'junit', name: 'junit', version: '4.13']
  )
}

//defaultTasks 'reportResults', 'coverageReport'

buildConfig {
  packageName("com.nwalsh")
  buildConfigField('String', 'TITLE', "\"${sincludeTitle}\"")
  buildConfigField('String', 'VERSION', "\"${sincludeVersion}\"")
  buildConfigField('String', 'SAXON_VERSION', "\"${saxonVersion}\"")
}

jar {
  archiveBaseName = "sinclude-${sincludeVersion}"
  manifest {
    attributes "Built-By": "Norman Walsh"
    attributes "Implementation-Vendor": "Norman Walsh"
    attributes "Implementation-Title": "Saxon XInclude Processor"
    attributes "Implementation-Version": sincludeVersion
  }
}

task distCopyJar(type: Copy, dependsOn: ['jar']) {
  from "${buildDir}/libs/${basename}-${sincludeVersion}.jar"
  into "${buildDir}/${basename}-${sincludeVersion}/lib"
  doFirst {
    mkdir "${buildDir}/${basename}-${sincludeVersion}/lib"
  }
}

task distCopyReadme(type: Copy) {
  from "README.org"
  into "${buildDir}/${basename}-${sincludeVersion}"
  doFirst {
    mkdir "${buildDir}/${basename}-${sincludeVersion}"
  }
}

task zipDist(type: Zip,
             dependsOn: ['distCopyReadme', 'distCopyJar']) {
  from("${buildDir}/${basename}-${sincludeVersion}")
  into "${basename}-${sincludeVersion}"
  archiveFileName = "${basename}-${sincludeVersion}.zip"
  doFirst {
    mkdir "${buildDir}/distributions"
  }
}

task dist(dependsOn: ['test', 'zipDist']) {
  doLast {
    println("Built dist for ${basename} version ${sincludeVersion}")
  }
}

// ============================================================

task helloWorld() {
  doLast {
    println('Hello, world.')
  }
}
